#>Summons new Anim Model if it doesn't exists
execute unless entity @e[tag=anim_main_select] at @p[tag=anim_user] run function anim_edit:gui/main/run/new_model
#>Makes selected Anim Model glow
data modify entity @e[tag=anim_main_select,limit=1] Marker set value 0b
data modify entity @e[tag=anim_main_select,limit=1] Glowing set value 1b

#>Loads Anim Editor Menu
execute unless score $main_page _anim matches -2147483648..2147483647 run scoreboard players set $main_page _anim 1
scoreboard players set $show_menu _anim 4
#>Title
tellraw @p[tag=anim_user] ["",{"text":"\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\nAnim Model","underlined":true},{"text":" "},{"nbt":"ver","storage":"anim:editor","color":"aqua"},"\n"]
#>Name
execute unless score $_curr_air _anim matches 1 run tellraw @p[tag=anim_user] [{"text":"Anim model name: ","color":"gray","hoverEvent":{"action": "show_text","contents":{"text":"Click to set a new name","color":"gray"}},"clickEvent": {"action":"suggest_command","value":"/data modify storage anim:editor anim_model_cname set value "}},{"nbt":"CustomName","entity":"@e[tag=anim_main_select,limit=1]","interpret":true,"color":"green"}]
execute if score $_curr_air _anim matches 1 run tellraw @p[tag=anim_user] [{"text":"Anim model name: ","color":"gray","hoverEvent":{"action": "show_text","contents":{"text":"Stay in air block!","color":"red"}}},{"text":"Err","color":"red"}]
#>ID (FIX)
tellraw @p[tag=anim_user] [{"text":"Anim model ID:","color":"gray","hoverEvent":{"action": "show_text","contents":{"text":"The assigned ID for this model (Currently non-changable, planned feature)","color":"gray"}}},{"text":" <"}," ",{"score":{"name":"@e[tag=anim_main_select,limit=1]","objective":"_anim_ida"},"color":"white"},{"text":" >"}]
#>Render connected parts
tellraw @p[tag=anim_user] {"text":"\nConnected anim parts:","color":"gray"}
function anim_edit:gui/main/run/menu4/get/start
#>Current Page
execute if score $main_page _anim matches ..1 run tellraw @p[tag=anim_user] [{"text":"Page:","color":"yellow"},{"text":" <","color":"gray"}," ",{"score":{"name":"$main_page","objective": "_anim"},"color":"white"}," ",{"text":"> ","clickEvent":{"action": "run_command","value":"/function anim_edit:gui/main/run/page_up"}}]
execute if score $main_page _anim matches 2.. run tellraw @p[tag=anim_user] [{"text":"Page:","color":"yellow"},{"text":" <","clickEvent":{"action": "run_command","value":"/function anim_edit:gui/main/run/page_down"}}," ",{"score":{"name":"$main_page","objective": "_anim"},"color":"white"}," ",{"text":"> ","clickEvent":{"action": "run_command","value":"/function anim_edit:gui/main/run/page_up"}}]

#>Menu Buttons
# If new Anim Model and no anim parts connected:
execute unless entity @e[tag=anim_part_menu] if entity @e[tag=anim_main_select,tag=anim_main_new] run tellraw @p[tag=anim_user] ["",{"text":"[Cancel]","color":"red","hoverEvent":{"action":"show_text","contents":{"text":"Cancels and deletes the newly created anim models and anim parts","color":"gray"}},"clickEvent": {"action": "run_command","value": "/execute if entity @e[tag=anim_main_select,tag=anim_main_new] run function anim_edit:gui/main/run/menu4/cancel"}}," ",{"text":"[Next]","color":"gray","hoverEvent":{"action": "show_text","contents":[{"text":"Continue with next steps on creating an animation","color":"gray"},{"text":"\nRequires at least 1 connected anim_part","color":"red"}]}}]
# If new Anim Model and there are anim parts connected:
execute if entity @e[tag=anim_part_menu] if entity @e[tag=anim_main_select,tag=anim_main_new] run tellraw @p[tag=anim_user] ["",{"text":"[Cancel]","color":"red","hoverEvent":{"action":"show_text","contents":{"text":"Cancels and deletes the newly created anim models and anim parts","color":"gray"}},"clickEvent": {"action": "run_command","value": "/execute if entity @e[tag=anim_main_select,tag=anim_main_new] run function anim_edit:gui/main/run/menu4/cancel"}}," ",{"text":"[Next]","color":"green","hoverEvent":{"action": "show_text","contents":{"text":"Continue with next steps on creating an animation","color":"gray"}},"clickEvent":{"action":"run_command","value":"/execute if entity @e[tag=anim_main_select,tag=anim_main_new] if entity @e[tag=anim_part_menu] run function anim_edit:gui/main/run/menu4/next"}}]

# If not new Anim Model and no anim parts connected:
execute unless entity @e[tag=anim_part_menu] if entity @e[tag=anim_main_select,tag=!anim_main_new] run tellraw @p[tag=anim_user] ["",{"text":"[Back]","color":"red","hoverEvent":{"action":"show_text","contents":{"text":"Deselect anim model and go back to main menu","color":"gray"}},"clickEvent": {"action": "run_command","value": "/execute if entity @e[tag=anim_main_select,tag=!anim_main_new] run function anim_edit:gui/main/run/menu4/back"}}," ",{"text":"[Delete]","color":"red","hoverEvent":{"action":"show_text","contents":{"text":"Deletes anim model and it's anim parts","color":"red"}},"clickEvent": {"action": "run_command","value": "/execute if entity @e[tag=anim_main_select,tag=!anim_main_new] run function anim_edit:gui/main/run/menu4/delete_confirm"}}," ",{"text":"[Duplicate]","color":"aqua","hoverEvent":{"action":"show_text","contents":{"text":"Duplicate anim model, its anim parts and animations to a new anim model","color":"gray"}},"clickEvent": {"action": "run_command","value": "/execute if entity @e[tag=anim_main_select,tag=!anim_main_new] run function anim_edit:gui/main/run/menu4/duplicate"}}," ",{"text":"[Save]","color":"gray","hoverEvent":{"action":"show_text","contents":[{"text":"Saves data and goes back to animations menu","color":"gray"},{"text":"\nRequires at least 1 connected anim_part","color":"red"}]}}]
# If not new Anim Model and there are anim parts connected:
execute if entity @e[tag=anim_part_menu] if entity @e[tag=anim_main_select,tag=!anim_main_new] run tellraw @p[tag=anim_user] ["",{"text":"[Back]","color":"red","hoverEvent":{"action":"show_text","contents":{"text":"Deselect anim model and go back to main menu","color":"gray"}},"clickEvent": {"action": "run_command","value": "/execute if entity @e[tag=anim_main_select,tag=!anim_main_new] run function anim_edit:gui/main/run/menu4/back"}}," ",{"text":"[Delete]","color":"red","hoverEvent":{"action":"show_text","contents":{"text":"Deletes anim model and it's anim parts","color":"red"}},"clickEvent": {"action": "run_command","value": "/execute if entity @e[tag=anim_main_select,tag=!anim_main_new] run function anim_edit:gui/main/run/menu4/delete_confirm"}}," ",{"text":"[Duplicate]","color":"aqua","hoverEvent":{"action":"show_text","contents":{"text":"Duplicate anim model, its anim parts and animations to a new anim model","color":"gray"}},"clickEvent": {"action": "run_command","value": "/execute if entity @e[tag=anim_main_select,tag=!anim_main_new] run function anim_edit:gui/main/run/menu4/duplicate"}}," ",{"text":"[Save]","color":"green","hoverEvent":{"action":"show_text","contents":{"text":"Saves data and goes back to animations menu","color":"gray"}},"clickEvent": {"action": "run_command","value": "/execute if entity @e[tag=anim_part_menu] if entity @e[tag=anim_main_select,tag=!anim_main_new] run function anim_edit:gui/main/run/menu4/save"}}]

#>Gamerule
function anim_edit:gamerule